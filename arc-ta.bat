@ECHO OFF
REM --------------------------------------------------------------------
REM Этот скрипт должен стать DataDriven.
REM
REM * работает только с файлами изменёнными после 31.05.2016
REM   параметр -ta
REM
REM предпологается:
REM 1. наличие расширенной обработки команд DOS
REM 2. версия RAR 3.62 (без параметра -idc должна пойти 3.42)
REM переменные:
REM A - полное имя журнала работы скрипта
REM B - полное имя базового каталога архивации
REM C - число созданных, после вызова :ROTATE, архивов
REM      устанавливается 0 :ROTATE, +1 :ARC
REM D - полное имя текущего подкаталога в ротации
REM E - код последней ошибки = возвращаемое программой значение
REM F - 0 - частичный архив; 1 - полный
REM I - номер текущего подкаталога в ротации
REM J - номер текущей архивации
REM L - полное имя списка архивации
REM M - число подкаталогов в ротации
REM N - макс. число архиваций в подкаталоге
REM P - имя файла скрипта, т.е. этой программы
REM R - команда вызова архиватора
REM S - полное имя inc-файла
REM X - полное имя списка исключений архивации
REM Z - полное имя файла комментария архива
REM --------------------------------------------------------------------
SETLOCAL ENABLEDELAYEDEXPANSION
CD D:\Archive\arc2016
D:
SET B=D:\Archive\arc2016
SET A=%B%\arc.log
SET E=0
SET M=3
SET N=30
SET P=%0
ECHO -- %P% start %DATE% >>                                          %A%
:
CALL :ROTATE comp1
IF %ERRORLEVEL% EQU 0 (
 CALL :ARC c1 "\\COMP1\share1"
 CALL :ARC c2 "\\COMP1\share2"
 CALL :POST
)
:
CALL :ROTATE srv
IF %ERRORLEVEL% EQU 0 (
 CALL :ARC s1 "\\SERVER1\share1"
 CALL :ARC s2 "\\SERVER1\share2"
 CALL :POST
)
:
ECHO %TIME% exit %E% >>                                              %A%
EXIT /B %E%
REM ====================================================================
:
:POST
REM --------------------------------------------------------------------
REM пост-обработка; вынесена для красоты вызывающего кода
REM пишет в журнал число архивов созданное для данного вызова :ROTATE
REM --------------------------------------------------------------------
IF %C% GTR 0 (
 CALL :CREATE_INC
 ECHO + %C% >>                                                       %A%
) ELSE (
 ECHO - %C% >>                                                       %A%
)
GOTO :EOF
REM ====================================================================

:ROTATE
REM --------------------------------------------------------------------
REM ротация подкаталогов с архивами; %M% подкаталогов по %N% _архиваций_
REM вызов: CALL :ROTATE <dir>
REM  где <dir> - основа имени подкаталогов в ротации и inc-файла
REM устанавливает:
REM  %C% = 0
REM  %D% - полное имя текущего подкаталога для записи архивов
REM  %S% - полное имя inc-файла
REM  значения %I% и %J% устанавливает но не записывает в inc-файл
REM   запись выполняется после архивации если были созданы архивы и
REM   не было критических ошибок
REM возвращает не 0 если произошла критическая ошибка
REM 
REM примечание:
REM  1. Ошибка удаления архивов из подкаталога не считается критической.
REM     Соответственно, возвращается 0.
REM  2. В случае ошибок, счётчики сразу записываются в inc-файл.
REM     Т.о. есть шанс что однажды мы перейдём к другой папке,
REM     в которой возможно ошибка не повторится
REM --------------------------------------------------------------------
IF "%1" EQU "" (
 SET E=101
 EXIT /B %E%
)
SET C=0
SET D=
SET I=%M%
SET J=%N%
SET S=%B%\%1.inc.bat
IF NOT EXIST "%S%" CALL :CREATE_INC
CALL "%S%"
REM способ удалить пробелы вокруг чисел, преобразовать строки в числа:
SET /A I=%I%
SET /A J=%J%
IF %I% LSS 1 SET I=1
IF %J% LSS 1 SET J=1
SET /A J+=1
REM если лимит архиваций исчерпан - выполнить ротацию
IF %J% GTR %N% (
 SET J=1
 SET /A I+=1
 IF !I! GTR %M% SET I=1
)
SET D=%B%\%1_%I%
REM если папки нет - создать
IF NOT EXIST "%D%" (
 SET J=1
 MD "%D%"
 IF ERRORLEVEL 1 (
  SET E=%ERRORLEVEL%
  ECHO MD "%D%" ERROR %E% >>                                         %A%
  CALL :CREATE_INC
  EXIT /B %E%
 )
)
ECHO - %1 %I%:%J% >>                                                 %A%
REM требуется чистка папки
IF %J% EQU 1 IF EXIST "%D%\*.rar" (
 DEL /F /Q "%D%\*.rar"
 IF ERRORLEVEL 1 (
  SET E=%ERRORLEVEL%
  ECHO DEL /F /Q "%D%\*.rar" ERROR %E% >>                            %A%
 )
)
IF %J% EQU 1 CALL :CREATE_INC
EXIT /B 0
REM ====================================================================
:
:CREATE_INC
REM --------------------------------------------------------------------
REM записывает значения сётчиков в "inc-файл" для их сохранения между
REM  вызовами программы
REM
REM примечание:
REM  Сохранённое таким образом число после CALL <inc-файл>
REM  окажется строкой с концевым пробелом. Лечится так:
REM   SET /A I=%I%
REM --------------------------------------------------------------------
ECHO @ECHO OFF >                                                     %S%
ECHO REM Do not edit! Maintained by script %P% >>                    %S%
ECHO SET I=%I% >>                                                    %S%
ECHO SET J=%J% >>                                                    %S%
GOTO :EOF
REM ====================================================================
:
:ARC
REM --------------------------------------------------------------------
REM вызов: CALL :ARC <имя> <сетевой_ресурс>
REM где <имя> основа имени архивов <имя>_1.rar без пробелов
REM
REM увеличивает счётчик созданных архивов %C%
REM --------------------------------------------------------------------
NET USE | FIND /I "T:" > NUL
IF %ERRORLEVEL% EQU 0 NET USE T: /DELETE
NET USE T: %2 /PERSISTENT:NO
IF ERRORLEVEL 1 (
 SET E=%ERRORLEVEL%
 ECHO NET USE T: %2 ERROR %E% >>                                     %A%
 GOTO :EOF
)
CALL :ENV_INIT %D%\%1_ %2
%R% >                                                                %O%
IF ERRORLEVEL 2 (
 SET E=%ERRORLEVEL%
 ECHO %R% %D%\%1_ @%L% ERROR %E% >>                                  %A%
REM не ошибка: нет файлов
) ELSE IF ERRORLEVEL 1 (
 REM если создавался полный архив, всё-таки ошибка, файлы должны быть
 IF %F% EQU 1 SET E=%ERRORLEVEL%
 FIND /I "Нет файлов" < %O%
 REM это какое-то другое предупреждение - записать его в журнал
 IF ERRORLEVEL 1 (
  TYPE %O% >>                                                        %A%
  SET E=1
 ) ELSE (
  ECHO  нет файлов >>                                                %A%
 )
REM архив создан - увеличить счётчик
) ELSE (
 SET /A C+=1
 ECHO +1
)
NET USE T: /DELETE
GOTO :EOF
REM ====================================================================
:
:ENV_INIT
REM --------------------------------------------------------------------
REM инициализация переменных и файлов с параметрами
REM 
REM вызов:
REM  CALL :ENV_INIT <base>
REM  ECHO archive comments >>                            %Z%
REM  ECHO mask_to_arc >>                                 %L%
REM  ECHO mask_to_exclude >>                             %X%
REM  %R% >                                               %O%
REM      mask_to_arc - путь для включения в архив
REM      может быть несколько строк ECHO, или не быть вообще
REM      <base> - полный путь и общая часть имени файла архива
REM               например, D:\Arc\srv1\obmen_ для файлов:
REM               D:\Arc\srv1\obmen_20150224.rar
REM               D:\Arc\srv1\obmen_20150225.rar
REM
REM  параметры RAR:
REM  ac          Снять атрибут "Архивный" после архивации или извлечения
REM  ao          Добавить файлы с установленным атрибутом "Архивный"
REM  ag[формат]  Добавить к имени архива текущие дату и время
REM  idp         Не отображать процентный индикатор хода выполнения работы
REM  idq         Активизирует "тихий" режим, при котором на экран
REM  m2          Метод сжатия - быстрый (1 - скоростной)
REM  md          Размер словаря в КБ (4096 - максимальный)
REM  ms          Расширения файлов для архивирования без сжатия
REM  tl          Устанавливать время архива по времени новейшего файла
REM  rr          Добавить информацию для восстановления
REM  sv          Создать независимые непрерывные тома
REM  v4G         Создавать тома размером 4Gb
REM  x@<список>  Не обрабатывать файлы/шаблоны, указанные в файле-списке
REM  y           Автоматически отвечать "да" на все запросы
REM  z<файл>     Прочитать комментарий архива из файла
REM --------------------------------------------------------------------
SET F=1
SET L=%TEMP%\LIST.TMP
SET O=%TEMP%\RAROUT.TMP
SET X=%TEMP%\EXCL.TMP
SET Z=%TEMP%\DESC.TMP
DEL %L% > NUL
DEL %O% > NUL
DEL %X% > NUL
DEL %Z% > NUL
SET R=rar a -ac 
REM если не первая архивация в данном подкаталоге - создать частичный архив
IF %J% NEQ 1 IF EXIST %1*.rar SET F=0
IF %F% EQU 0 (
 SET R=%R% -ao
 ECHO partial archive #%J% >                                         %Z%
 ECHO %TIME% %2 changes >>                                           %A%
) ELSE (
 ECHO full archive #%J% >                                            %Z%
 ECHO %TIME% %2 full >>                                              %A%
)
ECHO T:\ >                                                           %L%
ECHO *.gid >                                                         %X%
ECHO *\Thumbs.db >>                                                  %X%
ECHO ~*.tmp >>                                                       %X%
ECHO ~$*.doc >>                                                      %X%
SET R=%R% -agYYYYMMDDHHMMSS -idc -idp -ilog%B%\rarerr.log -m2 -ms[7z;avi;gif;jpeg;jpg;mp3;mp4;png;rar;wmv;vob;zip] -md4096 -r -rr -sv -ta20160531 -tl -v4G -x@%X% -y -z%Z% %1 @%L%
REM ====================================================================
REM 20160314 nvvpost@yandex.ru